plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
}

sourceCompatibility = 11

repositories {
    mavenCentral()
}

sourceSets {
	componentTest {
		java.srcDir file('src/component-test/java')
		resources.srcDir file('src/component-test/resources')
	}
}

task componentTest(type: Test) {
	testClassesDirs = sourceSets.componentTest.output.classesDirs
	classpath = sourceSets.componentTest.runtimeClasspath
}

componentTest.dependsOn jar
check.dependsOn componentTest

dependencies {
    implementation(platform("org.springframework.boot:spring-boot-dependencies:2.3.3.RELEASE"))

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'io.micrometer:micrometer-registry-prometheus'

    testImplementation("org.junit.jupiter:junit-jupiter:5.6.2")
    testImplementation 'org.assertj:assertj-core:3.12.1'

    componentTestImplementation("org.junit.jupiter:junit-jupiter:5.6.2")
    componentTestImplementation 'org.assertj:assertj-core:3.12.1'
    componentTestImplementation 'org.testcontainers:testcontainers:1.14.3'
    componentTestImplementation 'io.rest-assured:rest-assured:4.1.2'
}

mainClassName = 'lt.rieske.demo.Application'

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
